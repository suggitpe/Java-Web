import static groovy.io.FileType.DIRECTORIES


allprojects {
    defaultTasks 'clean', 'build'
}

subprojects { subprojects ->

    apply plugin: 'war'
    apply plugin: 'idea'
    apply plugin: 'jetty'

    group = 'org.suggs.java-web'
    version = '1.0-SNAPSHOT'
    buildDir = 'target'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    idea.module {
        downloadSources = true
    }

    configurations {
        wsimport
    }

    ext.genDir = "$buildDir/generated-src"
    sourceSets.main.java.srcDirs += genDir

    task wsimport(dependsOn: processResources) {
        ext.generatedJavaPackage = 'org.suggs.ws'
        doLast {
            file(genDir).mkdirs()
            sourceSets.main.output.classesDir.mkdirs()

            ant.taskdef(
                    name: 'wsimport',
                    classname: 'com.sun.tools.ws.ant.WsImport',
                    classpath: configurations.wsimport.asPath
            )

            sourceSets.main.output.resourcesDir.eachFileRecurse(DIRECTORIES) {
                it.eachFileMatch(~/.*\.wsdl/) { File file ->
                    println("Running wsimport for ${file}")
                    ant.wsimport(
                            keep: true,
                            package: generatedJavaPackage,
                            wsdl: file,
                            destDir: sourceSets.main.output.classesDir,
                            sourcedestdir: genDir
                    )
                }
            }
        }
    }

    task wsgen(dependsOn: processResources){
        doLast{
            sourceSets.main.java.resourcesDir.mkdirs()
        }

    }

    dependencies {
        wsimport "com.sun.xml.ws:jaxws-tools:2.1.4"

        compile "org.slf4j:slf4j-api:$slf4jVersion"

        runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
        runtime "log4j:log4j:$log4jVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }

    jar {
        manifest {
            attributes 'Implementation-Title': description,
                    'Implementation-Version': version
        }
    }


    test {
        include '**/*Test.*'
        exclude '**/*WebTest.*'
    }

    task webTest(type: Test, dependsOn: test) {

        systemProperties 'concordion.output.dir': file("${buildDir}/concordion")

        include '**/*WebTest.*'
        include '**/*Scenario.*'

        doFirst {
            logger.quiet("Starting the jetty webserver with web app")
            jettyRunWar.daemon = true
            jettyRunWar.execute()
        }
        doLast {
            logger.quiet "Stopping jetty web application"
            jettyStop.execute()
        }
    }
}

project("build-pipeline") {

    description = "A working example of how to do functional testing against a web application"
    dependencies {
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"

        runtime "org.codehaus.jackson:jackson-mapper-asl:$jacksonMapperVersion"
        runtime "com.thoughtworks.xstream:xstream:$xstreamVersion"

        testCompile("org.concordion:concordion:$concordionVersion") { exclude group: "junit" }
        testCompile "org.agileinsider:concordion-plus:$concordionPlusVersion"
        testCompile "info.cukes:cucumber-junit:$cucumberVersion"
        testCompile "info.cukes:cucumber-java:$cucumberVersion"
        testCompile("org.jbehave:jbehave-core:$jbehaveVersion") { exclude group: "junit" }
        testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
        testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    }

    //[jettyRun, jettyRunWar, jettyStop]*.httpPort = 10099
    //[jettyRun, jettyRunWar, jettyStop]*.stopPort = 10099
    //[jettyRun, jettyRunWar, jettyStop]*.stopKey = 'stopKey'
    jettyRun.httpPort = 10099
}

project("sandbox:sandbox-webservices:jax-rs-simple") {
    dependencies {
        compile "com.sun.jersey:jersey-bundle:1.17.1"
    }
}

project("sandbox:sandbox-webservices:jax-ws-simple:client") {
    wsimport.generatedJavaPackage = 'org.suggs.sandbox.jaxws.simple.client.bindings'
    compileJava.dependsOn wsimport
}

project("sandbox:sandbox-webservices:jax-ws-simple:service") {
}